extends Area3D

# TODO Check if this is best approach
@export var attack: Attack
## The distance below the current checkpoint before the player dies
@export var fall_margin: float = 30.0

## Entity to be followed for fall boundary tracking; set via controlled_entity_changed
@export var target: Node3D

# Store the exact spot the player was born before any checkpoints existed
var _fallback_spawn: Vector3


func _ready() -> void:
	assert(target != null, "Target missing in " + name)

	body_entered.connect(_on_body_entered)
	CheckpointManager.checkpoint_activated.connect(_on_checkpoint_activated)
	GameEvents.controlled_entity_changed.connect(_on_controlled_entity_changed)
	if GameEvents.controlled_entity != null:
		_on_controlled_entity_changed(GameEvents.controlled_entity)
	call_deferred("_initialize_position")


func _physics_process(_delta: float) -> void:
	assert(target != null, "Target missing in " + name)

	global_position.x = target.global_position.x
	global_position.z = target.global_position.z


func _initialize_position() -> void:
	_fallback_spawn = target.global_position

	if CheckpointManager.has_active_checkpoint():
		global_position.y = CheckpointManager.get_respawn_position().y - fall_margin
	else:
		global_position.y = target.global_position.y - fall_margin


func _on_checkpoint_activated(checkpoint_position: Vector3) -> void:
	global_position.y = checkpoint_position.y - fall_margin


func _on_controlled_entity_changed(entity: Node3D) -> void:
	assert(entity != null, "Controlled entity missing in " + name)
	target = entity


func _on_body_entered(body: Node3D) -> void:
	if body != target:
		return

	var target_position: Vector3 = _fallback_spawn
	if CheckpointManager.has_active_checkpoint():
		target_position = CheckpointManager.get_respawn_position()

	# TODO Check if a check if in group players here is needed
	body.respawn(2.0, target_position)
